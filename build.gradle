plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'dev.vanengine'
version = '0.1.16'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
    withJavadocJar()
    withSourcesJar()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:4.0.2'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://central.sonatype.com/repository/maven-snapshots/' }
}

dependencies {
    api "dev.vanengine:van-java-core:${version}"
    api 'org.springframework.boot:spring-boot-starter-webmvc'
    compileOnly 'org.springframework.boot:spring-boot-autoconfigure'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = 'dev.vanengine'
            artifactId = 'van-spring-boot-starter'

            pom {
                name = 'Van Spring Boot Starter'
                description = 'Spring Boot integration for the Van template engine'
                url = 'https://github.com/van-java/van-spring-boot-starter'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'van'
                        name = 'Van Contributors'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/van-java/van-spring-boot-starter.git'
                    developerConnection = 'scm:git:ssh://github.com/van-java/van-spring-boot-starter.git'
                    url = 'https://github.com/van-java/van-spring-boot-starter'
                }
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

signing {
    def signingKey = System.getenv('GPG_PRIVATE_KEY')
    def signingPassword = System.getenv('GPG_PASSPHRASE')
    required { signingKey }
    if (signingKey) {
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications.maven
}
